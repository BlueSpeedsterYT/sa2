SECTIONS {
    . = 0x2000000;
    EWRAM (NOLOAD) :
    ALIGN(4)
    {
    }

    . = 0x203B000;
    .text :
    ALIGN(4) {
        build/payload/asm/crt0.o(.text);
        build/payload/asm/multi_sio_asm.o(.text);
        build/payload/src/main.o(.text);
        build/payload/src/multi_sio.o(.text);
        build/payload/src/sio32_multi_load.o(.text);
        build/payload/asm/libagbsyscall.o(.text);
        *libgcc.a:_call_via_rX.o(.text);
        *libgcc.a:_udivsi3.o(.text);
        *libgcc.a:_dvmd_tls.o(.text);
        *libc.a:memcpy.o(.text);
    }

    .rodata :
    ALIGN(4) {
        build/payload/data/data.o(.rodata);
    }

    . = 0x03000000;
    IWRAM (NOLOAD) :
    ALIGN(4)
    {
        . = 0x00000000; gIntrTable = .;
        . = 0x00000040; gMultiSioRecv = .;
        . = 0x00000090; gMultiSioStatusFlags = .;
        . = 0x000000A0; gIntrMainBuf = .;
        . = 0x000004A0; gMultiSioSend = .;
        . = 0x000004A2; gUnknown_030004A2 = .;
        . = 0x000004C0; gMultiSioIntrFuncBuf = .;
        . = 0x000005E0; gMultiSioArea = .;
        . = 0x000007D0; gMultiSioRecvFuncBuf = .;
        . = 0x00000810; gSio32MultiLoadArea = .;
        . = 0x00007F00; gUnknown_03007F00 = .;
        . = 0x00007FA0; gUnknown_03007FA0 = .;
        . = 0x00007FFA; gUnknown_03007FFA = .;
        . = 0x00007FFC; gUnknown_03007FFC = .;
    }

    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
