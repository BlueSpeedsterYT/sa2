SECTIONS {
    . = 0x2000000;
    EWRAM (NOLOAD) :
    ALIGN(4)
    {
    }

    . = 0x203B000;
    .text :
    ALIGN(4) {
        build/payload/asm/crt0.o(.text);
        build/payload/asm/multi_sio_asm.o(.text);
        build/payload/src/main.o(.text);
        build/payload/src/multi_sio.o(.text);
        build/payload/src/sio32_multi_load.o(.text);
        build/payload/asm/libagbsyscall.o(.text);
        *libgcc.a:_call_via_rX.o(.text);
        *libgcc.a:_udivsi3.o(.text);
        *libgcc.a:_dvmd_tls.o(.text);
        *libc.a:memcpy.o(.text);
    }

    .rodata :
    ALIGN(4) {
        build/payload/src/main.o(.rodata);
        build/payload/src/multi_sio.o(.rodata);
        build/payload/src/sio32_multi_load.o(.rodata);
        build/payload/src/loader_tables.o(.rodata);
        build/payload/data/data.o(.rodata);
    }

    . = 0x03000000;
    IWRAM (NOLOAD) :
    ALIGN(4)
    {
        build/payload/src/main.o(.data);
        build/payload/src/multi_sio.o(.data);
        build/payload/src/sio32_multi_load.o(COMMON);
    }

    /* DWARF debug sections.
       Symbols in the DWARF debugging sections are relative to the beginning
       of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
